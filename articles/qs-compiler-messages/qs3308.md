---
description: Describes the error message and resolution for Q# compiler warning QS3308. 
author: bradben
ms.author: brbenefield
ms.date: 04/01/2023
ms.service: azure-quantum
ms.subservice: core
ms.topic: troubleshooting-problem-resolution
no-loc: ['Q#', '$$v', '$$', "$$", '$', "$", $, $$, '\cdots', 'bmatrix', '\ddots', '\equiv', '\sum', '\begin', '\end', '\sqrt', '\otimes', '{', '}', '\text', '\phi', '\kappa', '\psi', '\alpha', '\beta', '\gamma', '\delta', '\omega', '\bra', '\ket', '\boldone', '\\\\', '\\', '=', '\frac', '\text', '\mapsto', '\dagger', '\to', '\begin{cases}', '\end{cases}', '\operatorname', '\braket', '\id', '\expect', '\defeq', '\variance', '\dd', '&', '\begin{align}', '\end{align}', '\Lambda', '\lambda', '\Omega', '\mathrm', '\left', '\right', '\qquad', '\times', '\big', '\langle', '\rangle', '\bigg', '\Big', '|', '\mathbb', '\vec', '\in', '\texttt', '\ne', '<', '>', '\leq', '\geq', '~~', '~', '\begin{bmatrix}', '\end{bmatrix}', '\_']
title: Q# compiler message QS3308
uid: microsoft.quantum.qscompiler-diagnostics.QS3308
---

# QS3308

> [!NOTE]
> This error applies only to the Classic QDK.

This compiler warning is raised when deprecated syntax is used in a function or an operation to declare an array. For example, the following operation will raise warning QS3308:

```qsharp
operation MeasureAll(qs : Qubit[]) : Result[] {
    mutable results = new Result[Length(qs)];
                   // ^^^^^^^^^^^^^^^^^^^^^^
                   // |
                   // Raises warning QS3308.
    for idx in IndexRange(qs) {
        set results w/ idx <- M(qs[idx]);
    }
    return results;
}
```

This compiler error can be resolved by updating your Q# code to the latest version either manually, or using the Q# formatter provided with the [Visual Studio Code extension](https://marketplace.visualstudio.com/items?itemName=quantum.quantum-devkit-vscode).

## Error text

Deprecated syntax. Use `[]` to construct an empty array, or `[x, size = n]` to construct an array of `x` repeated `n` times.

## Details

This warning message is raised when using the `new` keyword to declare an array of a given size, such as `new Int[10]`. Using the `new` keyword in this way can be unsafe, as the array may contain invalid elements. For example, the following snippet fails at runtime:

```qsharp
function FailureCase() : Unit {
    let fns = new (Int -> Bool)[1];
    let fn = fns[0];
    Message($"{fn(42)}");
}
```

To prevent errors of this form, as of Q# version 0.21.2111.177148, the `new` keyword has been deprecated in favor of the `size` keyword.  There is no need to specify the data type, as the Q# compiler will detect the type from first parameter. Empty arrays can be specified as `[]` rather than `new 'T[0]`.  

The following example shows different ways to declare arrays.

```qsharp
    mutable results = [Zero, size = Length(qs)];   // an array of type Result with a calculated size
    let stats = [37, size = 4];                    // a four element array of type Int
    mutable dataList = [];                         // an empty array
```

## Suggestions

To update code using the deprecated `new` keyword to use the `size` keyword requires specifying the initial value for each element in the array being defined. For example, consider the [`Zipped` function](xref:Microsoft.Quantum.Arrays.Zipped); previously to Q# 0.21.2111.177148, `Zipped` used the `new` keyword to preallocate its output array:

```qsharp
function Zipped<'T, 'U>(left : 'T[], right : 'U[]) : ('T, 'U)[] {
    let nElements = Length(left) < Length(right)
                    ? Length(left)
                    | Length(right);
    mutable output = new ('T, 'U)[nElements];
                  // ^^^ raises QS3308

    for (idxElement in 0 .. nElements - 1) {
        set output w/= idxElement <- (left[idxElement], right[idxElement]);
    }

    return output;
}
```

This code was updated to remove the `new` keyword by specifying the initial element as `(left[0], right[0])`, and to return the empty array `[]` when `Length(left) == 0` or `Length(right) == 0`:

```qsharp
function Zipped<'T, 'U>(left : 'T[], right : 'U[]) : ('T, 'U)[] {
    let nElements = Length(left) < Length(right)
                    ? Length(left)
                    | Length(right);

    if nElements == 0 {
        return [];
    }

    mutable output = [(left[0], right[0]), size = nElements];

    for idxElement in 1 .. nElements - 1 {
        set output w/= idxElement <- (left[idxElement], right[idxElement]);
    }

    return output;
}
```

## See also

- [`ConstantArray` function](xref:Microsoft.Quantum.Arrays.ConstantArray)
